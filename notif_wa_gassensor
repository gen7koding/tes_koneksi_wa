#include <ESP8266WiFi.h>
#include <WiFiClientSecure.h>
#include <Wire.h>
#include <ESP8266HTTPClient.h>

#define GAS_SENSOR_PIN A0       // Sensor MQ-2 di A0
#define BUZZER_PIN 14     //GPIO14       
#define LED_MERAH 5 // GPIO5 alias D1
#define LED_HIJAU 4 //GPIO4 alias D2

char ssid[] = "S24+Gen7"; //ganti ssid wifi yang tersedia
char pass[] = "xxxxxxxx"; //ganti password wifi aktif

String nomorAdmin = "628122xxxx";  
String apiKeyAdmin = "84xxxx"; // masukkan key API yang dikirim bot
bool sudahKirim = false;

void setup() {
  Serial.begin(115200);

  pinMode(BUZZER_PIN, OUTPUT);
  digitalWrite(BUZZER_PIN, HIGH);
  delay (500);
  digitalWrite(BUZZER_PIN, LOW);
  pinMode(LED_MERAH, OUTPUT);
  pinMode(LED_HIJAU, OUTPUT);

  delay (10000); // pemanasan sensor gas
  WiFi.begin(ssid, pass);
  Serial.print("Menghubungkan WiFi");

  int count = 0;
  while (WiFi.status() != WL_CONNECTED && count < 10) {
    delay(1000);
    Serial.print(".");
    count++;
  }

  if (WiFi.status() == WL_CONNECTED) {
    Serial.println("\n‚úÖ Terhubung ke WiFi!");
  } else {
    Serial.println("\n‚ùå Gagal konek WiFi!");
  }
}

void loop() {
  int gasValue = analogRead(GAS_SENSOR_PIN);
  Serial.println("Gas Value: " + String(gasValue));

  if (gasValue > 900) { // batas nilai dapat di ganti
    // Alarm aktif
    for (int i = 0; i < 3; i++) { //membunyikan nada alarm
      digitalWrite(BUZZER_PIN, HIGH);
      delay(200);
      digitalWrite(BUZZER_PIN, LOW);
      delay(200);
    }

    digitalWrite(LED_MERAH, HIGH);
    digitalWrite(LED_HIJAU, LOW);

    if (!sudahKirim) {
      String pesan = "üö® Alarm Gas! Kebocoran gas terdeteksi. Segera periksa!";
      kirimWhatsApp(nomorAdmin, pesan, apiKeyAdmin);
      sudahKirim = true;
    }
  } else {
    // Aman
    digitalWrite(BUZZER_PIN, LOW);
    digitalWrite(LED_MERAH, LOW);
    digitalWrite(LED_HIJAU, HIGH);
    sudahKirim = false;
  }
  delay(1000);
}

void kirimWhatsApp(String nomor_hp, String pesan, String apikey) {
  if (WiFi.status() == WL_CONNECTED) {
    WiFiClientSecure client;
    client.setInsecure(); 

    HTTPClient https;

    pesan.replace(" ", "%20");
    pesan.replace("!", "%21");

    String url = "https://api.callmebot.com/whatsapp.php?phone=" + nomor_hp +
                 "&text=" + pesan +
                 "&apikey=" + apikey;

    Serial.println("URL WA: " + url);

    if (https.begin(client, url)) {
      int httpCode = https.GET();

      if (httpCode > 0) {
        Serial.println("WA Terkirim ‚úÖ: HTTP Code = " + String(httpCode));
        Serial.println("Respon Server: " + https.getString());
      } else {
        Serial.println("‚ùå Gagal Kirim WA! HTTP Code = " + String(httpCode));
      }

      https.end();
    } else {
      Serial.println("‚ùå Gagal Mulai HTTPS Connection");
    }
  } else {
    Serial.println("‚ùå WiFi Belum Terhubung");
  }
}
